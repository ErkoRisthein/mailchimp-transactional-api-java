plugins {
    id 'java'
    id 'maven-publish'
    id 'org.openapi.generator' version '7.20.0'
}

group = 'io.github.erkoristhein.mailchimp'
version = '1.0.59'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

ext {
    jackson_version = "2.19.2"
    jackson_databind_nullable_version = "0.2.9"
    spring_web_version = "7.0.4"
    jakarta_annotation_version = "2.1.1"
    junit_version = "5.10.2"
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/transactional.json"
    outputDir = "$buildDir/generated"
    library = "resttemplate"
    apiPackage = "io.github.erkoristhein.mailchimp.api"
    modelPackage = "io.github.erkoristhein.mailchimp.model"
    invokerPackage = "io.github.erkoristhein.mailchimp"
    configOptions = [
        useJakartaEe: "true",
        hideGenerationTimestamp: "true"
    ]
    additionalProperties = [
        groupId: "io.github.erkoristhein.mailchimp",
        artifactId: "mailchimp-transactional-api-java"
    ]
    skipValidateSpec = true
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
    test {
        java {
            srcDir "$buildDir/generated/src/test/java"
        }
    }
}

compileJava.dependsOn tasks.named('openApiGenerate')
compileTestJava.dependsOn tasks.named('openApiGenerate')

dependencies {
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.springframework:spring-web:$spring_web_version"
    implementation "org.springframework:spring-context:$spring_web_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.jakarta.rs:jackson-jakarta-rs-json-provider:$jackson_version"
    implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'mailchimp-transactional-api-java'
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
